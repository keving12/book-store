buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'io.spring.dependency-management' apply false
}

group 'com.kgracie.mytutor'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
    }

    sourceCompatibility = 14

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += "--enable-preview"
    }
    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
    }
    tasks.withType(JavaExec) {
        jvmArgs += "--enable-preview"
    }

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--enable-preview'
                ]
                classpath = files()
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation("org.springframework.boot:spring-boot-starter-web") {
            exclude(module: 'javax.annotation-api')
            exclude(module: 'java-annotation')
            exclude(module: 'spring-boot-starter-logging')
        }

        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.0'
        implementation 'com.fasterxml.jackson.core:jackson-core:2.12.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.12.0'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.0'
        implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.12.0'

        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-engine"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"

    }
}


